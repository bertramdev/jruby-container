apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
version = '0.1.0.SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")



buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}


repositories {
	mavenLocal()
    jcenter()
    mavenCentral()

}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
        resources{
            
        }
    }
}

dependencies {
    testCompile 'org.codehaus.groovy:groovy-all:2.0.7'
	// compile "com.bertramlabs.plugins:asset-pipeline-core:2.0.13"
    
    // compile 'log4j:log4j:1.2.17'
	compile 'org.jruby:jruby-complete:1.7.18'
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'sass-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'jruby-container'
                    description 'Provides an isolated Jruby Execution Container that can fetch gems to run from rubygems in an isolated folder.'
                    url 'https://github.com/bertramdev/jruby-container'
                    scm {
                        url 'https://github.com/bertramdev/jruby-container'
                        connection 'scm:https://bertramdev@github.com/bertramdev/jruby-container.git'
                        developerConnection 'scm:git://github.com/bertramdev/jruby-container.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publications = ['maven']
    pkg {
        repo = 'jruby'
        userOrg = 'bertramlabs'
        name = 'jruby-container'
        licenses = ['Apache-2.0']
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
